#!/usr/bin/env coffee

# HOST = 'http://localhost:3000/'
HOST = 'http://www.lysertron.com/'

UglifyJS = require 'uglify-js'
request = require 'request'
path = require 'path'
fs   = require 'fs'
prompt = require 'prompt'
ncp = require('ncp').ncp
argv = require('optimist').argv

root = path.join path.dirname(fs.realpathSync(__filename)), '..'
lib  = path.join root, 'lib'

projectRoot = process.cwd()

authFilePath = path.join projectRoot, 'auth.json'

layerCompiler = require path.join(lib, 'layer')
manifest = require path.join(lib, 'manifest')

[command, options...] = argv._
command ?= 'server'

switch command
  when 'server'
    app = require path.join(lib, '/server')
    port = options[0] || 3001

    console.log "Started server at http://localhost:#{port}/"
    app.listen port

  when 'project'
    unless options[0]
      console.log "Project name required!"
      console.log "  lysertron project myProjectName"

    else
      source = path.join(root, 'example_project')
      dest   = path.join(projectRoot, options[0])
      ncp source, dest, (err) ->
        if err
          console.error err

        else
          console.log ''
          console.log "Lysertron project created:"
          console.log "  #{dest}"
          console.log ''
          console.log "cd into the directory then launch the server with simply:"
          console.log "  lysteron"

  when 'publish'
    authKeys =
      if fs.existsSync authFilePath
        JSON.parse fs.readFileSync(authFilePath)

      else
        prompt.start()
        keyMsg = 'Enter Your API Key - See http://lysertron.com/users/edit'
        prompt.get [keyMsg], (err, result) ->
          fs.writeFileSync authFilePath,
            """
            {
              "echonest": "ECHONEST_API_KEY_HERE",
              "lysertron": "#{result[keyMsg]}"
            }
            """
          
          fs.writeFileSync path.join(projectRoot, '.gitignore'), 'auth.json'

          console.log "API Key added to auth.json. Please try to publish again."

        null


    if authKeys
      [type, name] = options
      layers = []

      if type && name
        echoes = ["#{type}/#{name}"]
      else
        manifest.findLayers true, (err, layerPaths) -> layers = layerPaths
      
      for layerPath in layers
        do (layerPath, type, name) ->
          [type, name] = layerPath.split('/')

          if name == 'example'
            console.log "#{layerPath} not uploaded. Will not upload 'example'. Modify and rename the layer if you want to publish it."
            return

          code = layerCompiler.compile type, name

          request.post "#{HOST}layers.js", json: { token: authKeys.lysertron, layer: { code, location: type, name } }, (err, res, body) ->
            if err
              console.log "Error: #{err}"
            else if ! /^2/.test res.statusCode.toString()
              console.log "Server Error: HTTP #{res.statusCode}\n#{res.body}"
            else
              console.log "Uploaded: #{type} #{name}"